# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd
pkgver=19
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kernel26>=2.6.36' 'initscripts' 'sysvinit' 'udev' 'util-linux>=2.19')
makedepends=('docbook-xsl' 'libxslt' 'cryptsetup')
optdepends=('cryptsetup: required for encrypted block devices'
            'initscripts-systemd: native boot and initialization scripts'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
groups=('systemd')
options=(!libtool)
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release")
md5sums=('32de12b132a2f6c270d422d682362a91'
         'f9922299150b4adda7b89e10bca33033')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --with-rootdir=/ \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --disable-audit \
              --disable-gtk

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  install -dm755 "$pkgdir/etc/modules-load.d"

  echo "d /var/run/console 755 root root" > "$pkgdir/etc/tmpfiles.d/console.conf"

  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}

