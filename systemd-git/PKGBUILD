# Contributor: Dave Reisner <d@falconindy.com>
pkgname=systemd-git
pkgver=20100622
pkgrel=1
pkgdesc="systems Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-core' 'dbus-glib' 'kernel26-systemd' 'glibc' 'libcgroup' 'udev>=154')
makedepends=('git' 'libcap' 'vala' 'gtk2' 'docbook-xsl' 'pam' 'tcp_wrappers')
optdepends=('gtk2: for systemadm GUI')
provides=('sysvinit')
conflicts=('sysvinit')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # If gtk2 is found on the system, the graphical utilities will be
  # built. This can be explicitly disabled with --disable-gtk.
  ./bootstrap.sh --prefix=/usr --libexecdir=/usr/lib

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  # make symlinks to classic sysvinit tools
  mkdir "$pkgdir/sbin"
  ln -s /bin/systemd "$pkgdir/sbin/init"
  for exe in reboot halt poweroff shutdown telinit runlevel; do
    ln -s "/bin/systemctl" "$pkgdir/sbin/$exe"
  done
}
