# Contributor: Dave Reisner <d@falconindy.com>

pkgname=util-linux-git
pkgver=20110812
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('bash' 'zlib')
makedepends=('git')
provides=('util-linux-ng=2.20' 'util-linux=2.20')
conflicts=('util-linux' 'util-linux-ng')
replaces=('util-linux-ng')
options=('!libtool')
optdepends=('perl: for chkdupexe support')

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
_gitname="util-linux"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -e 's@etc/adjtime@var/lib/hwclock/adjtime@' -i hwclock/hwclock.c

  msg "Starting configure..."
  ./autogen.sh
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-partx \
    --enable-libmount-mount

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/var/lib/hwclock"

  # if you don't have sysvinit installed, this can be commented out
  rm -f "$pkgdir/bin/mountpoint"
  rm -f "$pkgdir/usr/share/man/man1/mountpoint.1"
}

