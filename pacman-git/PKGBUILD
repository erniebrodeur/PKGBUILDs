# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-git
(( 42 )) && pkgname=(pacman-git pacman-contrib-git)
pkgver=20111228
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
makedepends=('git' 'asciidoc' 'curl>=7.19.4' 'gpgme' 'libarchive>=2.7.1')
checkdepends=('python2')
options=(!libtool !strip)
source=(pacman.conf
        pacman.conf.x86_64
        makepkg.conf)
md5sums=('e72cb52013729f893a5fd20b4c3be71d'
         '89ecb8f589ae0fe86411ba049784fc7c'
         '3c94d228b3d8611793a9b407fa87f3b9')

_gitroot="git://projects.archlinux.org/pacman.git"
_gitname="pacman"

build() {
  msg 'Connecting to GIT server...'

  if [[ -d $_gitname ]]; then
    ( cd $_gitname && git pull origin )
    msg 'The local files are updated.'
  else
    git clone $_gitroot
  fi

  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  rm -rf $_gitname-build
  git clone $_gitname{,-build}
  cd $_gitname-build

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-doc \
    --enable-git-version --enable-debug
  make
  make -C contrib
}

check() {
  cd "$srcdir/$_gitname-build"

  make check
}

package_pacman-git() {
  pkgdesc="A library-based package manager with dependency support. git version."
  groups=('base')
  depends=('bash' 'curl>=7.19.4' 'gpgme' 'libarchive>=2.7.1' 'pacman-mirrorlist')
  optdepends=('fakeroot: for makepkg usage as normal user')
  backup=(etc/pacman.conf etc/makepkg.conf)
  conflicts=('pacman')
  provides=('pacman')

  cd "$srcdir/$_gitname-build"

  make DESTDIR=$pkgdir install

  # install Arch specific stuff
  mkdir -p $pkgdir/etc
  case "$CARCH" in
    i686)
      install -m644 $srcdir/pacman.conf $pkgdir/etc/pacman.conf
      ;;
    x86_64)
      install -m644 $srcdir/pacman.conf.x86_64 $pkgdir/etc/pacman.conf
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  case "$CARCH" in
    i686)
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686 "
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64 "
      ;;
  esac
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # install completion files
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 contrib/bash_completion $pkgdir/etc/bash_completion.d/pacman
  mkdir -p $pkgdir/usr/share/zsh/site-functions/
  install -m644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}

package_pacman-contrib-git() {
  depends=('pacman')
  provides=('pacman-contrib')
  conflicts=('pacman-contrib')
  pkgdesc="Utilities for use with the pacman package manager"

  paccontrib=(
    bacman
    paccache
    pacdiff
    paclist
    pacscripts
    pacsearch
    paclog-pkglist
    pacsysclean
  )

  cd "$srcdir/$_gitname-build/contrib"
  for script in "${paccontrib[@]}"; do
    install -Dm755 ${script} ${pkgdir}/usr/bin/${script}
  done

  install -Dm644 PKGBUILD.vim ${pkgdir}/usr/share/vim/vimfiles/syntax/PKGBUILD.vim
  install -dm755 ${pkgdir}/usr/share/vim/vimfiles/ftdetect
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    > ${pkgdir}/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim
}

# vim: set ts=2 sw=2 et:
