# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd-git
pkgver=20110513
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'initscripts' 'kernel26>=2.6.36' 'sysvinit' 'udev' 'util-linux>=2.19')
makedepends=('docbook-xsl' 'git' 'libxslt' 'cryptsetup')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts-systemd-git: native boot and initialization scripts'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units-git: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
provides=('systemd')
conflicts=('systemd')
groups=('systemd')
options=(!libtool)
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/console.conf
        usr/lib/tmpfiles.d/legacy.conf
        usr/lib/tmpfiles.d/systemd.conf
        usr/lib/tmpfiles.d/x11.conf)
install=systemd.install
source=('os-release')
md5sums=('752636def0db3c03f121f8b4f44a63cd')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr \
               --libexecdir=/usr/lib \
               --disable-audit \
               --disable-gtk
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  install -dm755 "$pkgdir/etc/modules-load.d"
  printf "d /run/console 755 root root\n" > "$pkgdir/etc/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # install target doesn't bring in plymouth units
  install -m644 units/plymouth-* "$pkgdir/lib/systemd/system"

  # rename man pages to avoid conflicts with sysvinit
  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}
