# Contributor: Dave Reisner <d@falconindy.com>

pkgname=gocode-git
pkgver=20110102
pkgrel=1
pkgdesc="An autocompletion/refactoring daemon for the Go programming language (for use in Vim)"
arch=(i686 x86_64)
url="http://github.com/nsf/gocode"
license=('GPL')
depends=('go-hg')
provides=('gocode')
conflicts=('gocode')
makedepends=('git')
options=(!strip)

_gitroot="git://github.com/nsf/gocode.git"
_gitname="gocode"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    ( cd "$_gitname" && git pull origin; )
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  install -Dm755 gocode "$pkgdir/usr/bin/gocode"
  install -Dm644 vim/autoload/gocomplete.vim "$pkgdir/usr/share/vim/vimfiles/autoload/gocomplete.vim"
  install -Dm644 vim/ftplugin/go.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/go.vim"
}
