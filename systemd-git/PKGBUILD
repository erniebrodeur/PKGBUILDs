# Contributor: Dave Reisner <d@falconindy.com>

pkgname=systemd-git
pkgver=20100829
pkgrel=1
pkgdesc="systems Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-git' 'dbus-glib' 'kernel26-systemd' 'glibc' 'libcap' 'libcgroup-git' 
         'pam' 'sysvinit' 'tcp_wrappers' 'udev-systemd')
makedepends=('docbook-xsl' 'git' 'gtk2' 'libxslt' 'vala-devel')
optdepends=('gtk2: for systemadm GUI')
provides=('systemd')
conflicts=('systemd')
install=systemd.install

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap.sh --prefix=/usr --libexecdir=/usr/lib

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}
