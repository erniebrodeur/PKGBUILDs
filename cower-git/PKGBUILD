# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower-git
pkgver=20110206
pkgrel=1
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('MIT')
depends=('curl' 'pacman' 'yajl' )
makedepends=('git' 'perl')
conflicts=('cower')
provides=('cower')

_gitroot="git://github.com/falconindy/cower.git"
_gitname="cower"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  readelf -s /usr/lib/libalpm.so | grep -q alpm_find_satisfier &&
    PMCHECK="-D_HAVE_ALPM_FIND_SATISFIER"

  readelf -s /usr/lib/libalpm.so | grep -q alpm_db_register_local &&
    PMCHECK+=" -D_HAVE_ALPM_DB_REGISTER_LOCAL"

  readelf -s /usr/lib/libalpm.so | grep -q alpm_db_get_pkgcache_list &&
    PMCHECK+=" -D_HAVE_ALPM_DB_GET_PKGCACHE_LIST"

  make PMCHECK="$PMCHECK"
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
