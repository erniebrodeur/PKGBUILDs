# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod-git
pkgver=20120121
pkgrel=1
pkgdesc="interface to kernel module operations"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git/"
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('git')
conflicts=('kmod' 'module-init-tools')
provides=('kmod=5' 'module-init-tools=3.16')
replaces=('module-init-tools')
options=('!libtool')
backup=('lib/depmod.d/search.conf')
source=('depmod-search.conf')
md5sums=('4b8cbcbc54b9029c99fd730e257d4436')

_gitroot="git://git.profusion.mobi/kmod.git"
_gitname="kmod"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    ( cd "$_gitname" && git pull origin )
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_gitname-build"
  cp -r "$_gitname"{,-build}
  cd "$_gitname-build"

  ./autogen.sh
  ./configure \
    --sysconfdir=/etc \
    --bindir=/usr/bin \
    --with-rootprefix= \
    --with-zlib

  make
}

check() {
  make -C "$_gitname-build" check
}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install

  # tool directories
  install -dm755 "$pkgdir"/{,s}bin

  # config directories
  install -dm755 "$pkgdir"/{etc,lib}/{depmod,modprobe}.d

  # tool symlinks
  ln -s /usr/bin/kmod "$pkgdir/bin/lsmod"
  for tool in {ins,rm,dep}mod mod{info,probe}; do
    ln -s ../usr/bin/kmod "$pkgdir/sbin/$tool"
  done

  # install depmod.d file for search/ dir
  install -m644 "$srcdir/depmod-search.conf" "$pkgdir/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
