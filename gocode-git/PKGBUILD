# Contributor: Dave Reisner <d@falconindy.com>

pkgname=gocode-git
pkgver=20100908
pkgrel=1
pkgdesc="An autocompletion/refactoring daemon for the Go programming language (for use in Vim)"
arch=(i686 x86_64)
url="http://github.com/nsf/gocode"
license=('GPL')
depends=('go-hg')
provides=('gocode')
conflicts=('gocode')
makedepends=('git')

_gitroot="git://github.com/nsf/gocode.git"
_gitname="gocode"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
  install -m755 -D gocode "$pkgdir/usr/bin/gocode"

  cd vim
  install -m644 -D autoload/gocomplete.vim "$pkgdir/usr/share/vim/vimfiles/autoload/gocomplete.vim"
  install -m644 -D ftplugin/go.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/go.vim"
  install -m644 -D plugin/gocode.vim "$pkgdir/usr/share/vim/vimfiles/plugin/gocode.vim" 
}
