# Contributor: Dave Reisner <d@falconindy.com>

pkgname=valgrind-svn
pkgver=12384
pkgrel=1
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
url="http://valgrind.org/"
license=('GPL')
depends=('glibc' 'perl')
makedepends=('gdb')
provides=('valgrind')
conflicts=('valgrind')
source=('automake-paths.patch')
md5sums=('4a3effe1b9c216f2ae951624705baff7')

_svntrunk="svn://svn.valgrind.org/valgrind/trunk"
_svnmod="valgrind"

build() {
  if [[ -d $_svnmod/.svn ]]; then
    ( cd $_svnmod && svn up )
  else
    svn co "$_svntrunk" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/${_svnmod}-build"
  cd "$srcdir/${_svnmod}-build"

  # fix build for recent automake
  patch -Np0 <$srcdir/automake-paths.patch

  ./autogen.sh

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  if [[ "$CARCH" = x86_64 ]]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi

  make
}

check() {
  make -C "$_svnmod-build" check
}

package() {
  make -C "$_svnmod-build" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
