# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>

pkgname=vpnc-svn
pkgver=481
pkgrel=1
pkgdesc="VPN client for Cisco and Nortel VPN Concentrators - SVN trunk build"
arch=('i686' 'x86_64')
url="http://www.unix-ag.uni-kl.de/~massar/vpnc/"
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2')
makedepends=('subversion')
provides=(vpnc)
conflicts=(vpnc)
backup=(etc/vpnc/default.conf)
source=(vpnc.conf)
md5sums=('a7302cf6a12bdc1eebdcbd10b2df0faf')

vpnc_script_src='http://git.infradead.org/users/dwmw2/vpnc-scripts.git/blob/HEAD:/vpnc-script'

_svntrunk=http://svn.unix-ag.uni-kl.de/vpnc/trunk
_svnmod=vpnc

build() {
  cd "$srcdir"

  # im lazy -- grab the latest from git master
  curl -sS "$vpnc_script_src" > "$srcdir/vpnc-script"

  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up
  else
    svn co $_svntrunk --config-dir ./ $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # Build hybrid support
  sed -i 's|^#OPENSSL|OPENSSL|g' Makefile

  make PREFIX=/usr

}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -m755 "$srcdir/vpnc-script" "$pkgdir/etc/vpnc/vpnc-script"
  install -m644 "$srcdir/vpnc.conf" "$pkgdir/etc/vpnc/default.conf"
  rm -f "$pkgdir/etc/vpnc/vpnc.conf"
}
