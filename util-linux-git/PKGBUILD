# Contributor: Dave Reisner <d@falconindy.com>

pkgname=util-linux-git
pkgver=20120628
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('zlib' 'udev' 'ncurses')
makedepends=('git')
provides=('util-linux-ng=2.22' 'util-linux=2.22' 'eject')
conflicts=('util-linux-ng' 'util-linux' 'eject')
replaces=('util-linux-ng' 'eject')
options=('!libtool')

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
_gitname="util-linux"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname"{,-build}
  cd "$srcdir/$_gitname-build"

  msg "Configuring..."
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --disable-wall \
      --disable-kill \
      --enable-write \
      --enable-raw \
      --enable-fs-paths-extra="/usr/sbin:/usr/bin" \
      --enable-partx \
      --enable-chfn-chsh \
      --enable-vipw \
      --enable-newgrp

  msg "Starting make..."
  make
}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install

  # enable setuid bit on chfn and chsh (should be fixed upstream)
  chown 4755 "$pkgdir"/usr/bin/ch{sh,fn}

  # get rid of su for now
  rm "$pkgdir"/bin/su

  # uncomment this line if you're using sysvinit
  #rm "$pkgdir"/usr/share/man/man8/sulogin.8 "$pkgdir"/sbin/sulogin
}

