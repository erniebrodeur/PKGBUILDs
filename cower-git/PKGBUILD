# Contributor: Dave Reisner <d@falconindy.com>

pkgname=cower-git
pkgver=20100818
pkgrel=1
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('GPL')
depends=('curl' 'pacman' 'yajl' )
makedepends=('git')
conflicts=('cower')
provides=('cower')

_gitroot="git://github.com/falconindy/cower.git"
_gitname="cower"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
