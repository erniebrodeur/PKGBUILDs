# Contributor: Dave Reisner <d@falconindy.com>
pkgname=systemd-git
pkgver=20100713
pkgrel=1
pkgdesc="systems Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-git' 'dbus-glib' 'kernel26-systemd' 'glibc' 'libcgroup-git' 'udev>=160' 'libcap')
makedepends=('docbook-xsl' 'git' 'gtk2' 'libxslt' 'vala')
optdepends=('gtk2: for systemadm GUI')
provides=('sysvinit' 'systemd')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap.sh --prefix=/usr --libexecdir=/usr/lib || return 1

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install || return 1
  cd "$pkgdir"
  mkdir -p cgroup/{cpuset,ns,net_cls,cpu,cpuacct,memory,devices,freezer}
}
