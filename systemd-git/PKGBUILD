# Contributor: Dave Reisner <d@falconindy.com>

pkgname=systemd-git
pkgver=20100909
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-systemd' 'dbus-glib' 'kernel26-systemd' 'sysvinit' 'tcp_wrappers' 'udev-systemd')
makedepends=('docbook-xsl' 'git' 'gtk2' 'libcgroup-git' 'libxslt' 'vala-devel')
optdepends=('gtk2: systemadm GUI'
            'systemd-arch-units: collection of native unit files for Arch daemon/init scripts'
            'initscripts-systemd: native boot and initialization scripts'
            'selinux-usr-libselinux: integration with security enhanced Linux')
provides=('systemd')
conflicts=('systemd')
groups=('systemd')
options=(!libtool)
install=systemd.install

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap.sh --prefix=/usr --libexecdir=/usr/lib

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}
