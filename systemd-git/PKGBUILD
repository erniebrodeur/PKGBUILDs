# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd-git
pkgver=20110110
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kernel26>=2.6.36' 'sysvinit' 'udev>=164' 'util-linux-ng-git')
makedepends=('docbook-xsl' 'git' 'libxslt')
optdepends=('initscripts-systemd: native boot and initialization scripts'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
provides=('systemd')
conflicts=('systemd')
groups=('systemd')
options=(!libtool)
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("os-release")
md5sums=('f9922299150b4adda7b89e10bca33033')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # we don't build the gtk tools right now because the dependencies
  # aren't worth the "reward". If you really want to build them,
  # you'll need gtk3, libnotify>=0.5, and vala>=0.10. Don't forget
  # to remove the --disable-gtk flag

  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib \
    --disable-audit --disable-gtk

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  install -d755 "$pkgdir/etc/modules-load.d"

  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}
