# $Id: PKGBUILD 81175 2010-05-26 23:13:44Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname='gstreamer0.10-bad-plugins-slim'
pkgver=0.10.18
pkgrel=5
arch=('i686' 'x86_64')
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.29')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.bz2)
sha256sums=('2f800127e00da0f157358f87b06563b3de23cbc630fbf6295b43405e52e02070')

build() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Bad Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  sed -e 's/gst sys ext/gst/' -i Makefile || return 1
}

package() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
  depends=("gstreamer0.10-bad=${pkgver}" "faad2>=2.7")
  provides=('gstreamer0.10-bad-plugins')
  conflicts=('gstreamer0.10-bad-plugins')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install || return 1
  make -C ext DESTDIR="${pkgdir}" install || return 1
  make -C sys DESTDIR="${pkgdir}" install || return 1
  make -C gst-libs DESTDIR="${pkgdir}" uninstall || return 1
}
