# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower
pkgver=3.0.3
pkgrel=2
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('MIT')
depends=('curl' 'yajl' 'pacman')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.gz"
        "avoid-yajl-complete-parse.patch")
md5sums=('f507d3c7d87022c23a569624e5bcd267'
         'a249c5e2bc8610819f2ae73aa4bb6c67')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 < "$srcdir/avoid-yajl-complete-parse.patch"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh
