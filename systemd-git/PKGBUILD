# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd-git
pkgver=20111230
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kbd' 'libcap' 'udev>=172' 'util-linux>=2.19')
makedepends=('cryptsetup' 'docbook-xsl' 'git' 'gperf' 'intltool' 'libxslt'
             'linux-api-headers')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts: legacy hostname, locale, and vconsole support'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units-git: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
provides=('systemd')
conflicts=('systemd')
groups=('systemd')
options=(!libtool)
backup=(etc/dbus-1/system.d/org.freedesktop.{hostname,login,locale,timedate,systemd}1.conf
        etc/systemd/{user,system}.conf
        etc/systemd/systemd-logind.conf)
install=systemd.install
source=('os-release')
md5sums=('752636def0db3c03f121f8b4f44a63cd')

_gitroot="git://anongit.freedesktop.org/systemd"
_gitname="systemd"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # don't unset locale in getty
  # https://bugzilla.redhat.com/show_bug.cgi?id=663900
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  # don't trigger ./configure automatically
  ./autogen.sh ac
  ./configure --prefix=/usr \
              --with-rootdir= \
              --with-rootlibdir=/lib \
              --libdir=/usr/lib \
              --libexecdir=/usr/lib/systemd \
              --localstatedir=/var \
              --sysconfdir=/etc \
              --disable-audit \
              --disable-gtk

  make

  # fix .so links in manpages
  sed -i 's|\.so halt\.8|.so man8/systemd.halt.8|' man/{halt,reboot,poweroff}.8
  sed -i 's|\.so systemd\.1|.so man1/systemd.1|' man/init.1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # rename man pages to avoid conflicts with sysvinit and initscripts
  cd "$pkgdir/usr/share/man"
  for manpage in man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8 man5/{vconsole,locale}.conf.5; do
    IFS='/' read section page <<< "$manpage"
    mv "$manpage" "$section/systemd.$page"
  done
}
