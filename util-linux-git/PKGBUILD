# Contributor: Dave Reisner <d@falconindy.com>

pkgname=util-linux-git
pkgver=20110201
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('bash' 'zlib')
makedepends=('git')
provides=('util-linux-ng=2.19' 'util-linux=2.19')
replaces=('util-linux-ng')
options=('!libtool')
optdepends=('perl: for chkdupexe support')

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
_gitname="util-linux"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg "Starting configure..."
  ./autogen.sh
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-partx

  make HAVE_SLN=yes ADD_RAW=yes
}

package() {
  cd "$srcdir/$_gitname-build"

  make HAVE_SLN=yes ADD_RAW=yes DESTDIR="${pkgdir}" install

  # remove files
  rm -f "${pkgdir}/bin/kill"
  rm -f "${pkgdir}/usr/share/man/man1/kill.1"
  rm -f "${pkgdir}/usr/share/man/man5/nfs.5"
  rm -f "${pkgdir}/usr/share/info/dir"
}

