# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=systemd-git
pkgver=20120221
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('acl' 'dbus-core' 'kbd' 'kmod' 'libcap' 'udev' 'util-linux')
makedepends=('cryptsetup' 'docbook-xsl' 'git' 'gperf' 'intltool' 'libxslt'
             'linux-api-headers' 'xz')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts: legacy hostname, locale, and vconsole support'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units-git: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
provides=('systemd')
conflicts=('systemd')
groups=('systemd')
options=(!libtool)
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/systemd-logind.conf
        etc/systemd/systemd-journald.conf)
install=systemd.install
source=('os-release')
md5sums=('752636def0db3c03f121f8b4f44a63cd')

_gitroot="git://anongit.freedesktop.org/systemd/systemd.git"
_gitname="systemd"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # don't unset locale in getty
  # https://bugzilla.redhat.com/show_bug.cgi?id=663900
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  ./autogen.sh
  ./configure --with-rootprefix= \
              --with-pamlibdir=/lib/security \
              --libdir=/usr/lib \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --sysconfdir=/etc \
              --disable-audit \
              --disable-gtk \
              --with-distro=arch

  make
}

package() {
  cd "$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # install external sd-{daemon,readahead} libraries
  install -m644 -t "$pkgdir/usr/share/doc/systemd" \
    src/sd-daemon.[ch] \
    src/readahead/sd-readahead.[ch]

  ln -s ../lib/systemd/systemd "$pkgdir/bin/systemd"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -i '1s|^\.so \(.*\)\.\([[:digit:]]\+\)|.so man\2/\1.\2|' {} +

  # rename man pages to avoid conflicts with sysvinit and initscripts
  cd "$pkgdir/usr/share/man"

  # not building this with systemd
  rm -f "man1/systemadm.1"

  manpages=(man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8
            man5/{hostname,{vconsole,locale}.conf}.5)

  for manpage in "${manpages[@]}"; do
    IFS='/' read section page <<< "$manpage"
    mv "$manpage" "$section/systemd.$page"
  done
}
