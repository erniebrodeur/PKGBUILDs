# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd
pkgver=15
pkgrel=4
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('dbus-core' 'kernel26>=2.6.36' 'sysvinit' 'udev>=164' 'util-linux-ng-git')
makedepends=('docbook-xsl' 'git' 'libxslt' 'vala-devel')
optdepends=('gtk2: systemadm GUI'
            'systemd-arch-units: collection of native unit files for Arch daemon/init scripts'
            'initscripts-systemd: native boot and initialization scripts'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'tcp_wrappers: filter remote access to services')
groups=('systemd')
options=(!libtool)
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release")
md5sums=('36011aa8593862ca78e3e909f6143570'
         'f9922299150b4adda7b89e10bca33033')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Running autoreconf..."
  autoreconf
  ./configure --prefix=/usr --with-rootdir=/ --sysconfdir=/etc --libexecdir=/usr/lib \
              --disable-audit --disable-gtk

  # remove --disable-gtk if you want to build systemadm

  msg2 "Starting make..."
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"

  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv "$manpage.8" "systemd.$manpage.8"
  done
}
