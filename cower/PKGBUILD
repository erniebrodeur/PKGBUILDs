# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower
pkgver=2.9.9
pkgrel=3
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('GPL')
depends=('curl' 'yajl' 'pacman')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.gz"
        "update-pkbuild-path.patch"
        "set_curlopt_nosignal.patch")
md5sums=('557c52ccca82c89118d892bd7a3e5786'
         '73b3fbb3b652a6d4f69cf11070f85688'
         '2d6b5c0a2a11ce13736ec566365cbae5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if readelf -s /usr/lib/libalpm.so | grep -q alpm_find_satisfier; then
    PMCHECK=-D_HAVE_ALPM_FIND_SATISFIER
  fi

  patch -Np1 < "$srcdir"/set_curlopt_nosignal.patch
  patch -Np1 < "$srcdir"/update-pkbuild-path.patch

  make PMCHECK=$PMCHECK
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR=${pkgdir} install
}

# vim: ft=sh syn=sh
