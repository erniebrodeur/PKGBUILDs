# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=kmod-git
pkgver=20111226
pkgrel=1
pkgdesc="interface to kernel module operations"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git/"
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('git')
conflicts=('kmod')
provides=('kmod')
options=('!libtool')

_gitroot="git://git.profusion.mobi/kmod.git"
_gitname="kmod"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh \
    --with-rootlibdir=/lib \
    --with-zlib
  make
}

check() {
  make -C "$srcdir/$_gitname-build" check
}

package() {
  make -C "$srcdir/$_gitname-build" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
